#
# Executes commands at login pre-zshrc.
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER="open"
fi

#
# Editors
#

if (( $+commands[nvim] )); then
  alias vim="nvim"
  export EDITOR="nvim"
else
  export EDITOR="vim"
fi

export VISUAL="$EDITOR"
export PAGER="less"

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG="en_US.UTF-8"
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS="-F -g -i -M -R -S -w -x4 -X -z-4"

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

#
# Temporary Files
#
#
# Set TMPDIR if the variable is not set/empty or the directory doesn't exist
if [[ -z "${TMPDIR}" ]]; then
  export TMPDIR="/tmp/zsh-${UID}"
fi

if [[ ! -d "${TMPDIR}" ]]; then
  mkdir -m 700 "${TMPDIR}"
fi

## Local changes
[[ -f "$HOME/.zprofile-local" ]] && source "$HOME/.zprofile-local"

## Custom programs
# fzf
if [[ -d "/usr/local/opt/fzf" ]]; then
  export PATH="$PATH:/usr/local/opt/fzf/bin"
fi

if (( $+commands[fd] )); then
  export FZF_DEFAULT_COMMAND="fd . $HOME"
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
  export FZF_ALT_C_COMMAND="fd -t d . $HOME"
fi

# Load rbenv automatically
if (( $+commands[rbenv] )); then
  eval "$(rbenv init -)"
fi

# Opam
if (( $+commands[opam] )); then
  eval "$(opam config env)"
fi

if [[ "$NO_SUDO" = "true" ]]; then
  # npm
  if (( $+commands[npm] )); then
    export NPM_CONFIG_PREFIX="$HOME/.npm-global"
    export PATH="$PATH:$NPM_CONFIG_PREFIX/bin"
  fi
fi
